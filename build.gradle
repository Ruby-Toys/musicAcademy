plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'java'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

subprojects {
    group = 'ruby'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'
    apply plugin: 'jacoco'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    test {
        useJUnitPlatform()
    }
}

project(":core") {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        // mysql
        runtimeOnly 'mysql:mysql-connector-java'
        // jpa
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
        // querydsl
        implementation 'com.querydsl:querydsl-core'
        api 'com.querydsl:querydsl-jpa'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    }

    // build tools 의 run 을 IntelliJ 로 설정할 때에는 추가
    // IntelliJ 로 설정했을 때에는 컴파일 시점에 해당 경로에 Q클래스가 생성되므로 clean 시 해당 경로에 있는 Q클래스를 지우기 위함
//    clean {
//        delete file('src/main/generated')
//    }
}

project(":api") {
    configurations {
        asciidoctorExt
    }

    ext {
        set('snippetsDir', file("build/generated-snippets"))
    }

    dependencies {
        implementation project(':core')
        // web
        implementation 'org.springframework.boot:spring-boot-starter-web'
        // validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        // modelMapper
        implementation 'org.modelmapper:modelmapper:3.1.0'
        // security
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'
        // email
        implementation 'org.springframework.boot:spring-boot-starter-mail'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        // rest docs
        asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    }

    test {
        useJUnitPlatform()
        outputs.dir snippetsDir
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacoco.exec")
        }
    }

    asciidoctor {
        inputs.dir snippetsDir
        configurations 'asciidoctorExt'
        dependsOn test
    }

    bootJar {
        dependsOn asciidoctor
        copy {
            from asciidoctor.outputDir
            into "src/main/resources/static/docs"
        }
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    jacocoTestReport {
        reports {
            // 원하는 리포트를 켜고 끌 수 있습니다.
            html.enabled true
            xml.enabled false
            csv.enabled false

//  각 리포트 타입 마다 리포트 저장 경로를 설정할 수 있습니다.
//  html.destination file("$buildDir/jacocoHtml")
//  xml.destination file("$buildDir/jacoco.xml")
        }
    }

    jacocoTestCoverageVerification {
        // 이 커버리지 기준은 이 글의 맨 아래에서 다시 설명하겠습니다.
        violationRules {
            rule {
                element = 'CLASS'

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.80
                }

                // 카카오페이, 메일서비스 관련 제외
                excludes = [
                        'ruby.api.response.kakao',
                        'ruby.api.service.MailService',
                        'ruby.api.service.KakaoPayService',
                        'ruby.api.service.PaymentService',
                        'ruby.api.scheduler'
                ]
            }
        }
    }
}